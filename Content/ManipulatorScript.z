class ManipulatorScript : ZilchComponent
{
    //If this is set then the manipulation joint created
    //will lock the rotation of the grabbed object.
    [Property]
    var LockRotation : Boolean = false;
    
    //The minimum distance we can drag the object towards
    //the camera during mouse scroll.
    [Property]
    var MinDistance : Real = 3.0;
    
    //The maximum distance we can drag the objet away from the camera.
    [Property]
    var MaxDistance : Real = 100.0;
    
    //The object that we are currently grabbing
    var SelectedObj : Cog = null;
    //The current distance away from the camera that
    //the object's grab point should be.
    var Distance : Real = 0.0;
    
    var ManipulatorJoint : ManipulatorJoint = null;
    var PhyGunJoint : PhyGunJoint = null;
    
    constructor() : base()
    {
    }
    
    function Initialize(initializer : CogInitializer)
    {
        //we only operator on a component with a camera viewport,
        //grab the viewport from that component
        var viewport = this.Owner.Camera.Viewport;
        
        Zero.Connect(viewport, Events.MouseMove, this.OnMouseMove);
        Zero.Connect(viewport, Events.MouseDown, this.OnMouseDown);
        Zero.Connect(viewport, Events.MouseUp, this.OnMouseUp);
        Zero.Connect(viewport, Events.MouseScroll, this.OnMouseScroll);
        Zero.Connect(Zero.Keyboard, Events.KeyDown, this.OnKeyDown);
    }
    
    function OnKeyDown(keyboardEvent : KeyboardEvent)
    {
        //we only do logic when 'P' is pressed right now
        if(keyboardEvent.Key != Keys.P)
        {
            return;
        }
        
        //if we have an object selected, we need to
        //deselect it before changing our joint type
        if(this.SelectedObj != null)
        {
            this.DeselectObj();
        }
        this.LockRotation = !this.LockRotation;
    }
    
    function OnMouseMove(viewportMouseEvent : ViewportMouseEvent)
    {
        this.UpdateDraggedObject(viewportMouseEvent);
    }
    
    function OnMouseDown(viewportMouseEvent : ViewportMouseEvent)
    {
        if(this.SelectedObj != null)
        {
            this.DeselectObj();
        }
        
        var ray = viewportMouseEvent.WorldRay;
        
        var selectedPoint = local Real3(0.0, 0.0, 0.0);
        
        //find the object we clicked, if there wasn't one don't do anything
        var castResults = this.Space.PhysicsSpace.CastRayResults(ray, 1);
        foreach(var castResult in castResults)
        {
            //save the object we hit
            this.SelectedObj = castResult.ObjectHit;
            //save the point at which we clicked the object
            selectedPoint = castResult.WorldPosition;
        }
        
        if(this.SelectedObj == null)
        {
            return;
        }
        
        //find how far away the pick point is (for keeping the object on a sphere around us)
        this.Distance = Math.Length(selectedPoint - this.Owner.Transform.Translation);
        
        if(this.LockRotation)
        {
            this.CreatePhyGun(this.SelectedObj,selectedPoint);
        }
        else
        {
            this.CreateManipulator(this.SelectedObj,selectedPoint);
        }
    }
    
    function OnMouseUp(viewportMouseEvent : ViewportMouseEvent)
    {
        this.DeselectObj();
    }
    
    function DeselectObj()
    {
        if(this.SelectedObj == null)
        {
            return;
        }
        
        //mark that we have no selected object, and destroy the joint
        this.SelectedObj = null;
        if(this.LockRotation)
        {
            this.PhyGunJoint.Owner.Destroy();
        }
        else
        {
            this.ManipulatorJoint.Owner.Destroy();
        }
        
        this.ManipulatorJoint = null;
        this.PhyGunJoint = null;
    }
    
    function OnMouseScroll(viewportMouseEvent : ViewportMouseEvent)
    {
        //if we do not have a selected object, there is nothing to drag
        if(this.SelectedObj == null)
        {
            return;
        }
        
        //alter our grab distance but clamp between our min and max distance
        this.Distance += viewportMouseEvent.Scroll.Y;
        this.Distance = Math.Clamp(this.Distance, this.MinDistance, this.MaxDistance);
        //now compute the new world position of the mouse
        var newWorldPoint = this.ComputeWorldPoint(viewportMouseEvent);
        //update the object we grabbed with the new world point
        if(this.LockRotation)
        {
            this.UpdatePhyGunPoint(newWorldPoint);
        }
        else
        {
            this.UpdateManipulatorPoint(newWorldPoint);
        }
    }
    
    function CreateManipulator(obj : Cog, point : Real3)
    {
        //build a new manipulator joint attached by the picked point and save it
        var jointCreator = Zero.JointBuilder.CreateInstance();
        jointCreator.ConnectToWorld = true;
        var cog = jointCreator.AttachWorld(obj,obj,point,"ManipulatorJoint");
        this.ManipulatorJoint = cog.ManipulatorJoint;
    }
        
    function CreatePhyGun(obj : Cog, point : Real3)
    {
        //build a new manipulator joint attached by the picked point and save it
        var jointCreator = Zero.JointBuilder.CreateInstance();
        jointCreator.ConnectToWorld = true;
        var cog = jointCreator.AttachWorld(obj,obj,point,"PhyGunJoint");
        this.PhyGunJoint = cog.PhyGunJoint;
    }
        
    function UpdateDraggedObject(viewportMouseEvent : ViewportMouseEvent)
    {
        //if we do not have a selected object, there is nothing to drag
        if(this.SelectedObj == null)
        {
            return;
        }
        
        var newWorldPoint = this.ComputeWorldPoint(viewportMouseEvent);
        
        if(this.LockRotation)
        {
            this.UpdatePhyGunPoint(newWorldPoint);
        }
        else
        {
            this.UpdateManipulatorPoint(newWorldPoint);
        }
    }
    
    function ComputeWorldPoint(viewportMouseEvent : ViewportMouseEvent) : Real3
    {
        //get the vector into the screen from where we clicked
        var viewPlaneNormal = viewportMouseEvent.RayDirection;
        //we want to keep the point on the object we clicked at a fixed distance from us,
        //so use the viewPlaneNormal to find the point on the sphere defined by the
        //initial picking distance
        return this.Owner.Transform.Translation + viewPlaneNormal * this.Distance;
    }
        
    function UpdateManipulatorPoint(worldPoint : Real3)
    {
        this.ManipulatorJoint.TargetPoint = worldPoint;
    }
        
    function UpdatePhyGunPoint(worldPoint : Real3)
    {
        this.PhyGunJoint.TargetPoint = worldPoint;
    }
}
